buildscript {
    repositories {
        maven { url 'https://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'de.topobyte:gradle-launch-and-install-plugin:0.0.5'
    }
}

apply plugin: 'de.topobyte.launch-and-install-gradle-plugin'

mainClassName = 'de.topobyte.jeography.executables.RunJeographyGIS'

startScripts.enabled = false

launchAndInstall {
    installationPath = 'topobyte/jeography-gis'
    makeselfLabel = 'Jeography GIS'
    createScript(project, 'de.topobyte.jeography.executables.RunJeographyGIS', 'jeography-gis')
    createScript(project, 'de.topobyte.jeography.executables.RunGeometryPreview', 'geometry-preview')
    createScript(project, 'de.topobyte.jeography.executables.CreatePlaceDatabase', 'create-place-database')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation project(':jeography-gis')

    runtimeOnly 'org.slf4j:slf4j-log4j12:1.6.1'
    runtimeOnly 'log4j:log4j:1.2.16'
}

distributions {
    main {
        distributionBaseName = 'jeography-gis'
    }
}

task copyJars(type: Copy) {
    into "$buildDir/lib-run"
    from jar
}

task copyDeps(type: Copy) {
    into "$buildDir/lib-run"
    from configurations.runtimeClasspath
}

task createRuntime(dependsOn: [copyJars, copyDeps]) {
    description 'Setup the execution environment for the scripts'
}

task jarMain(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveBaseName = 'jeography'
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task fatJars(dependsOn: ['jarMain']) {
}
