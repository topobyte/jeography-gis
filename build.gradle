wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':jeography-config'),
    project(':jeography-core'),
    project(':jeography-gis'),
    project(':jeography-places'),
    project(':jeography-tiles'),
    project(':jeography-tools'),
    project(':jeography-viewers')
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
}

configure(project(':jeography-testing')) {
    apply plugin: 'java-library'
}

configure(project(':jeography-gis-exe')) {
    apply plugin: 'application'
}

subprojects {
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        maven {
            url 'https://mvn.slimjars.com'
        }
        mavenCentral()
    }
}

configure(libraries) {
    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    java {
        withSourcesJar()
    }

    def pomInfo = new XmlSlurper().parse(new File(projectDir, 'info.pom'));

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "$pomInfo.name"
                    description = "$pomInfo.description"
                    url = "$pomInfo.url"
                }
            }
        }
    }
}
