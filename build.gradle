wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':jeography-config'),
    project(':jeography-core'),
    project(':jeography-gis'),
    project(':jeography-places'),
    project(':jeography-tiles'),
    project(':jeography-tools'),
    project(':jeography-viewers')
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

configure(project(':jeography-testing')) {
    apply plugin: 'java-library'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

configure(project(':jeography-gis')) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        maven {
            url 'https://mvn.slimjars.com'
        }
        mavenCentral()
    }
}

configure(libraries) {
    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    java {
        withSourcesJar()
    }

    def pomInfo = new XmlSlurper().parse(new File(projectDir, 'info.pom'));

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "$pomInfo.name"
                    description = "$pomInfo.description"
                    url = "$pomInfo.url"
                }
            }
        }
    }
}
