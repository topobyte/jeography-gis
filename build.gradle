wrapper {
    gradleVersion = '4.9'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        maven {
            url 'http://mvn.slimjars.com'
        }
        mavenCentral()
    }
}

def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        outputDir = new File(project.buildDir, 'scripts')
        mainClassName = mainClass
        applicationName = name
        classpath = project.tasks.startScripts.classpath
        if (!project.hasProperty('scripts')) {
            project.ext.scripts = []
        }
        project.ext.scripts.add(name)
    }

    project.tasks[name].dependsOn(project.jar)

    project.distributions.main.contents.from(project.tasks[name]) {
         duplicatesStrategy = DuplicatesStrategy.EXCLUDE
         into 'bin'
    }
}

def printScriptNames(project) {
    project.scripts.each {
        println it
    }
}
